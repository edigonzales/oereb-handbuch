plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id "de.undercouch.download" version "4.1.2"
}

apply from: "$rootDir/gradle/versioning.gradle"

repositories {
    jcenter()
    maven { url 'https://rubygems-proxy.torquebox.org/releases' }
    mavenCentral()
}

asciidoctor {
    sourceDir  file('src/docs/asciidoc')
    outputDir  file('build/docs')
}

asciidoctorj {  // or asciidoctor.asciidoctorj
    attributes \
        revnumber : project.version,
        imagesdir : "images"
   options doctype: 'book', ruby: 'erubis'
}

pdfThemes {
    github 'sogis-frutiger', { 
        organisation = 'sogis' 
        repository = 'sogis-docs-asciidoctor-themes' 
        relativePath = 'sogis-frutiger' 
        branch = 'master' 
    }
}

asciidoctorPdf {
    attributes \
        imagesdir : "src/docs/asciidoc/images"
    theme 'sogis-frutiger'
    fontsDir file("fonts/")
    doLast {
        file("${outputDir}/master.pdf").renameTo("${outputDir}/oereb-handbuch.pdf")
    }
}

task downloadFonts (type: Download) {
    src([
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-normal.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-bold.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-italic.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-bold_italic.ttf'
    ])
    dest file("fonts/")
}

task deleteFonts(type: Delete) {
    delete 'fonts'
}

//asciidoctorPdf.dependsOn downloadFonts
//asciidoctorPdf.finalizedBy deleteFonts